apply plugin: 'groovy'
apply plugin: "maven"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.2.0'
    compile "com.android.tools.build:gradle-api:2.2.0"
    compile "org.javassist:javassist:3.20.0-GA"
}

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

version = "0.9.9.5"
group = "com.letv.sarrsdesktop"

def siteUrl = 'https://github.com/seiginonakama/BlockCanaryEx'    // project homepage
def gitUrl = 'https://github.com/seiginonakama/BlockCanaryEx.git' // project git

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name 'blockcanaryex-plugin'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'lqcandqq13'
                        name 'lqcandqq13'
                        email 'lqcandqq13@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

def I = File.separator

task sourcesJar(type: Jar) {
    from new File(project.getProjectDir(), "src${I}main${I}groovy")
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

Properties properties = new Properties()
boolean isHasFile = false
if (project.rootProject.file('local.properties') != null){
    isHasFile = true
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = isHasFile ? properties.getProperty("bintray.user") : System.getenv("bintray.user")
    key = isHasFile ? properties.getProperty("bintray.apikey") : System.getenv("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "blockcanaryex-plugin"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}